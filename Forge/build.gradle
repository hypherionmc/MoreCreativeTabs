buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id "com.modrinth.minotaur" version "2.+"
    id "me.hypherionmc.cursegradle" version "2.0.1"
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

archivesBaseName = "${mod_name}-forge-${minecraft_version}"

minecraft {
    mappings channel: 'official', version: minecraft_version

    if (project.hasProperty('forge_ats_enabled') && project.findProperty('forge_ats_enabled').toBoolean()) {
        accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
        project.logger.debug('Forge Access Transformers are enabled for this project.')
    }

    runs {
        client {
            workingDirectory project.file('run')
            arg "-mixin.config=morecreativetabs.mixins.json"
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            mods {
                modClientRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            mods {
                modServerRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            taskName 'Data'
            mods {
                modDataRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    compileOnly project(":Common")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    testAnnotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

processResources {
    from project(":Common").sourceSets.main.resources
}


jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

mixin {
    add sourceSets.main, "morecreativetabs.refmap.json"
    config 'morecreativetabs.mixins.json'
}

modrinth {
    debugMode = false
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "nOiHJ1dx"
    versionNumber = "${minecraft_version}-${version}"
    versionType = "release"
    changelog = rootProject.file("changelog-forge.md").text.trim()
    uploadFile = jar
    versionName = "[FORGE ${minecraft_version}] MoreCreativeTabs - ${project.version}"
    gameVersions = ["1.18.1", "1.18.2"]
    loaders = [ "forge" ]
}

curseforge {
    if (System.getenv("CURSE_TOKEN") !== null) {
        apiKey = System.getenv("CURSE_TOKEN")
    } else {
        apiKey = ""
    }
    project {
        id = "581838"
        releaseType = "release"
        addGameVersion "1.18.2"
        addGameVersion "1.18.1"
        addGameVersion "Forge"
        addGameVersion "Java 17"
        changelogType = "markdown"
        changelog = rootProject.file("changelog-forge.md")
        mainArtifact(jar) {
            displayName = "[FORGE ${minecraft_version}] MoreCreativeTabs - ${project.version}"
        }
    }
    options {
        forgeGradleIntegration = false
        debug = false
        fabricIntegration = false
    }
}

afterEvaluate {
    tasks.curseforge581838.dependsOn reobfShadowJar
}
